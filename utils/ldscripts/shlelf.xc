/* Script for -z combreloc: combine and sort reloc sections */
OUTPUT_FORMAT("elf32-shl", "elf32-shl",
	      "elf32-shl")
OUTPUT_ARCH(sh)
STARTUP(_kos_startup.o)
INPUT(crti.o)
INPUT(crtbegin.o)
INPUT(crtend.o)
INPUT(crtn.o)
LOAD_OFFSET = DEFINED(LOAD_OFFSET) ? LOAD_OFFSET : 0x8c010000 ;
ICACHE_SIZE = 0x2000 ;

SECTIONS
{
  /* Read-only sections, merged into text segment: */
  PROVIDE (__executable_start = LOAD_OFFSET); . = LOAD_OFFSET;
  .text           :
  {
    *(.text .stub .text.* .gnu.linkonce.t.*)
    /* .gnu.warning sections are handled specially by elf32.em.  */
    *(.gnu.warning)
  } =0

  /* Custom sections, aligned to the icache size */
  .sub0 : ALIGN(ICACHE_SIZE) {
    __sub0_start = .;
    *(.sub0*)
    __sub0_end = .;
  }
  ASSERT(__sub0_end - __sub0_start <= ICACHE_SIZE, "Error: Sub-section .sub0 is bigger than icache size")
  .sub1 : ALIGN(ICACHE_SIZE) {
    __sub1_start = .;
    *(.sub1*)
    __sub1_end = .;
  }
  ASSERT(__sub1_end - __sub1_start <= ICACHE_SIZE, "Error: Sub-section .sub1 is bigger than icache size")
  .sub2 : ALIGN(ICACHE_SIZE) {
    __sub2_start = .;
    *(.sub2*)
    __sub2_end = .;
  }
  ASSERT(__sub2_end - __sub2_start <= ICACHE_SIZE, "Error: Sub-section .sub2 is bigger than icache size")
  .sub3 : ALIGN(ICACHE_SIZE) {
    __sub3_start = .;
    *(.sub3*)
    __sub3_end = .;
  }
  ASSERT(__sub3_end - __sub3_start <= ICACHE_SIZE, "Error: Sub-section .sub3 is bigger than icache size")
  .sub4 : ALIGN(ICACHE_SIZE) {
    __sub4_start = .;
    *(.sub4*)
    __sub4_end = .;
  }
  ASSERT(__sub4_end - __sub4_start <= ICACHE_SIZE, "Error: Sub-section .sub4 is bigger than icache size")
  .sub5 : ALIGN(ICACHE_SIZE) {
    __sub5_start = .;
    *(.sub5*)
    __sub5_end = .;
  }
  ASSERT(__sub5_end - __sub5_start <= ICACHE_SIZE, "Error: Sub-section .sub5 is bigger than icache size")
  .sub6 : ALIGN(ICACHE_SIZE) {
    __sub6_start = .;
    *(.sub6*)
    __sub6_end = .;
  }
  ASSERT(__sub6_end - __sub6_start <= ICACHE_SIZE, "Error: Sub-section .sub6 is bigger than icache size")
  .sub7 : ALIGN(ICACHE_SIZE) {
    __sub7_start = .;
    *(.sub7*)
    __sub7_end = .;
  }
  ASSERT(__sub7_end - __sub7_start <= ICACHE_SIZE, "Error: Sub-section .sub7 is bigger than icache size")
  .sub8 : ALIGN(ICACHE_SIZE) {
    __sub8_start = .;
    *(.sub8*)
    __sub8_end = .;
  }
  ASSERT(__sub8_end - __sub8_start <= ICACHE_SIZE, "Error: Sub-section .sub8 is bigger than icache size")
  .sub9 : ALIGN(ICACHE_SIZE) {
    __sub9_start = .;
    *(.sub9*)
    __sub9_end = .;
  }
  ASSERT(__sub9_end - __sub9_start <= ICACHE_SIZE, "Error: Sub-section .sub9 is bigger than icache size")

  .init           :
  {
    KEEP (*(.init))
  } =0
  .fini           :
  {
    KEEP (*(.fini))
  } =0
  .interp         : { *(.interp) }
  .note.gnu.build-id : { *(.note.gnu.build-id) }
  .hash           : { *(.hash) }
  .gnu.hash       : { *(.gnu.hash) }
  .dynsym         : { *(.dynsym) }
  .dynstr         : { *(.dynstr) }
  .gnu.version    : { *(.gnu.version) }
  .gnu.version_d  : { *(.gnu.version_d) }
  .gnu.version_r  : { *(.gnu.version_r) }
  .rel.dyn        :
    {
      *(.rel.init)
      *(.rel.text .rel.text.* .rel.gnu.linkonce.t.*)
      *(.rel.fini)
      *(.rel.rodata .rel.rodata.* .rel.gnu.linkonce.r.*)
      *(.rel.data.rel.ro* .rel.gnu.linkonce.d.rel.ro.*)
      *(.rel.data .rel.data.* .rel.gnu.linkonce.d.*)
      *(.rel.tdata .rel.tdata.* .rel.gnu.linkonce.td.*)
      *(.rel.tbss .rel.tbss.* .rel.gnu.linkonce.tb.*)
      *(.rel.ctors)
      *(.rel.dtors)
      *(.rel.got)
      *(.rel.sdata .rel.sdata.* .rel.gnu.linkonce.s.*)
      *(.rel.sbss .rel.sbss.* .rel.gnu.linkonce.sb.*)
      *(.rel.sdata2 .rel.sdata2.* .rel.gnu.linkonce.s2.*)
      *(.rel.sbss2 .rel.sbss2.* .rel.gnu.linkonce.sb2.*)
      *(.rel.bss .rel.bss.* .rel.gnu.linkonce.b.*)
    }
  .rela.dyn       :
    {
      *(.rela.init)
      *(.rela.text .rela.text.* .rela.gnu.linkonce.t.*)
      *(.rela.fini)
      *(.rela.rodata .rela.rodata.* .rela.gnu.linkonce.r.*)
      *(.rela.data .rela.data.* .rela.gnu.linkonce.d.*)
      *(.rela.tdata .rela.tdata.* .rela.gnu.linkonce.td.*)
      *(.rela.tbss .rela.tbss.* .rela.gnu.linkonce.tb.*)
      *(.rela.ctors)
      *(.rela.dtors)
      *(.rela.got)
      *(.rela.sdata .rela.sdata.* .rela.gnu.linkonce.s.*)
      *(.rela.sbss .rela.sbss.* .rela.gnu.linkonce.sb.*)
      *(.rela.sdata2 .rela.sdata2.* .rela.gnu.linkonce.s2.*)
      *(.rela.sbss2 .rela.sbss2.* .rela.gnu.linkonce.sb2.*)
      *(.rela.bss .rela.bss.* .rela.gnu.linkonce.b.*)
    }
  .rel.plt        : { *(.rel.plt) }
  .rela.plt       : { *(.rela.plt) }
  .plt            : { *(.plt) }
  PROVIDE (__etext = .);
  PROVIDE (_etext = .);
  PROVIDE (etext = .);
  .rodata         :
  {
    *(.rodata .rodata.* .gnu.linkonce.r.*)
    . = ALIGN(4);
    __tdata_align = .;
    LONG (ALIGNOF(.tdata));
    . = ALIGN(4);
    __tbss_align = .;
    LONG (ALIGNOF(.tbss));
    . = ALIGN(4);
  }
  .rodata1        : { *(.rodata1) }
  .sdata2         :
  {
    *(.sdata2 .sdata2.* .gnu.linkonce.s2.*)
  }
  .sbss2          : { *(.sbss2 .sbss2.* .gnu.linkonce.sb2.*) }
  .eh_frame_hdr : { *(.eh_frame_hdr) }
  .eh_frame       : ONLY_IF_RO {
    KEEP (*crtbegin.o(.eh_frame))
    KEEP (*(EXCLUDE_FILE (*crtend.o) .eh_frame))
    KEEP (*crtend.o(.eh_frame))
  }
  .gcc_except_table   : ONLY_IF_RO { *(.gcc_except_table .gcc_except_table.*) }
  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  */
  . = ALIGN(128) + (. & (128 - 1));
  /* Exception handling  */
  .eh_frame       : ONLY_IF_RW {
    KEEP (*crtbegin.o(.eh_frame))
    KEEP (*(EXCLUDE_FILE (*crtend.o) .eh_frame))
    KEEP (*crtend.o(.eh_frame))
  }
  .gcc_except_table   : ONLY_IF_RW { *(.gcc_except_table .gcc_except_table.*) }
  /* Thread Local Storage sections  */
  .tdata	  :
  {
    __tdata_start = .;
    *(.tdata .tdata.* .gnu.linkonce.td.*)
  }
  __tdata_size = SIZEOF(.tdata);
  .tbss	(NOLOAD)	  :
  {
    *(.tbss .tbss.* .gnu.linkonce.tb.*)
    *(.tcommon)
  }
  __tbss_size = SIZEOF(.tbss);
  .preinit_array     :
  {
    PROVIDE_HIDDEN (__preinit_array_start = .);
    KEEP (*(.preinit_array))
    PROVIDE_HIDDEN (__preinit_array_end = .);
  }
  .init_array     :
  {
     PROVIDE_HIDDEN (__init_array_start = .);
     KEEP (*(SORT(.init_array.*)))
     KEEP (*(.init_array))
     PROVIDE_HIDDEN (__init_array_end = .);
  }
  .fini_array     :
  {
    PROVIDE_HIDDEN (__fini_array_start = .);
    KEEP (*(.fini_array))
    KEEP (*(SORT(.fini_array.*)))
    PROVIDE_HIDDEN (__fini_array_end = .);
  }
  .ctors          :
  {
    ___ctors = .;
    /* gcc uses crtbegin.o to find the start of
       the constructors, so we make sure it is
       first.  Because this is a wildcard, it
       doesn't matter if the user does not
       actually link against crtbegin.o; the
       linker won't look for a file to match a
       wildcard.  The wildcard also means that it
       doesn't matter which directory crtbegin.o
       is in.  */
    KEEP (*crtbegin.o(.ctors))
    KEEP (*crtbegin?.o(.ctors))
    /* We don't want to include the .ctor section from
       the crtend.o file until after the sorted ctors.
       The .ctor section from the crtend file contains the
       end of ctors marker and it must be last */
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .ctors))
    KEEP (*(SORT(.ctors.*)))
    KEEP (*(.ctors))
    ___ctors_end = .;
  }
  .dtors          :
  {
    ___dtors = .;
    KEEP (*crtbegin.o(.dtors))
    KEEP (*crtbegin?.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o ) .dtors))
    KEEP (*(SORT(.dtors.*)))
    KEEP (*(.dtors))
    ___dtors_end = .;
  }
  .jcr            : { KEEP (*(.jcr)) }
  .data.rel.ro : { *(.data.rel.ro.local* .gnu.linkonce.d.rel.ro.local.*) *(.data.rel.ro* .gnu.linkonce.d.rel.ro.*) }
  .dynamic        : { *(.dynamic) }
  .data           :
  {
    *(.data .data.* .gnu.linkonce.d.*)
    SORT(CONSTRUCTORS)
  }
  .data1          : { *(.data1) }
  .got            : { *(.got.plt) *(.got) }
  /* We want the small data sections together, so single-instruction offsets
     can access them all, and initialized data all before uninitialized, so
     we can shorten the on-disk segment size.  */
  .sdata          :
  {
    *(.sdata .sdata.* .gnu.linkonce.s.*)
  }
  _edata = .; PROVIDE (edata = .);
  . = ALIGN(8);
  __monitors_start = .;
  .monitors       :
  {
    *(.monitors)
  }
  __monitors_end = .;
  . = ALIGN(8);
  __bss_start = .;
  .sbss           :
  {
    *(.dynsbss)
    *(.sbss .sbss.* .gnu.linkonce.sb.*)
    *(.scommon)
  }
  .bss            :
  {
   *(.dynbss)
   *(.bss .bss.* .gnu.linkonce.b.*)
   *(COMMON)
   /* Align here to ensure that the .bss section occupies space up to
      _end.  Align after .bss to ensure correct alignment even if the
      .bss section disappears because there are no input sections.
      FIXME: Why do we need it? When there is no .bss section, we don't
      pad the .data section.  */
   . = ALIGN(. != 0 ? 32 / 8 : 1);
  }
  . = ALIGN(32 / 8);
  . = ALIGN(32 / 8);
  _end = .; PROVIDE (end = .);
  .ocram 0x7c001000 (NOLOAD) :
  {
    *(.ocram)
    /* We have 8kb of operand cache RAM. The next line lets ld throw
       an error if we exceed that size.  */
    . = . > 0x2000 ? 0x2000 : .;
  }
  /* Stabs debugging sections.  */
  .stab          0 : { *(.stab) }
  .stabstr       0 : { *(.stabstr) }
  .stab.excl     0 : { *(.stab.excl) }
  .stab.exclstr  0 : { *(.stab.exclstr) }
  .stab.index    0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment       0 : { *(.comment) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
  /* DWARF 3 */
  .debug_pubtypes 0 : { *(.debug_pubtypes) }
  .debug_ranges   0 : { *(.debug_ranges) }
  .gnu.attributes 0 : { KEEP (*(.gnu.attributes)) }
  /DISCARD/ : { *(.note.GNU-stack) *(.gnu_debuglink) }
}
